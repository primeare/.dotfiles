#!/usr/bin/env fish

function pass --argument-names message;
  echo [(set_color green) OK (set_color normal)] $message
end

function fail --argument-names message;
  echo [(set_color red) FAIL (set_color normal)] $message
  exit 1
end

set INSTALLED_FILES_PATHS \
  "$HOME/.gitconfig" \
  "$HOME/.gitmessage" \
  "$HOME/.npmrc" \
  "$HOME/.config/fish/config.fish" \
  "$HOME/.config/starship.toml" \
  "$HOME/Library/Application Support/Code/User/settings.json"

set INSTALLED_DIRECTORIES_PATHS \
  "$HOME/.config/fish/functions"

whereis fish
fish --version
whereis starship
starship --version
whereis node
node --version
whereis code
code --version
whereis docker
docker --version

set VSCODE_INSTALLED_EXTENSIONS (code --list-extensions)
set NUMBER_OF_VSCODE_EXTENSIONS_INSTALLED (count $VSCODE_INSTALLED_EXTENSIONS)

for FILE in $INSTALLED_FILES_PATHS;
  # if a symbolic link and a non-empty file
  if test -L "$FILE" && test -s "$FILE"
    pass "$FILE is a symbolic link file"
  else
    fail "$FILE is not a symbolic link file"
  end
end

for DIRECTORY in $INSTALLED_DIRECTORIES_PATHS;
  # if a symbolic link and a directory
  if test -L "$DIRECTORY" && test -d "$DIRECTORY"
    pass "$DIRECTORY is a symbolic link directory"
  else
    fail "$DIRECTORY is not a symbolic link directory"
  end
end

if test $NUMBER_OF_VSCODE_EXTENSIONS_INSTALLED -gt 0
  pass "$NUMBER_OF_VSCODE_EXTENSIONS_INSTALLED Visual Studio Code extensions installed successfully"
else
  fail "Visual Studio Code extensions installation failed"
end
